allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url nexusURL
        credentials {
            username = nexusUser
            password = nexusPassword
        }
    }
}

artifacts {
    def appImage = file("./dist_electron/Loginized ${version}.AppImage")
    def debFile = file("./dist_electron/loginized_${version}_amd64.deb")
    // def snapFile = file("./dist_electron/loginized_${version}_amd64.snap")
    def pacmanFile = file("./dist_electron/loginized-${version}.pacman")
    def rpmFile = file("./dist_electron/loginized-${version}.x86_64.rpm")
    def zipFile = file("./dist_electron/loginized-${version}.zip")

    archives appImage, debFile, pacmanFile, rpmFile, zipFile
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: nexusURL) {
            authentication(userName: nexusUser, password: nexusPassword)
        }

        pom.project {
            name rootProject.name
            groupId 'juhaku.gnome'
            artifactId 'loginized'
            version = version
            description 'Gnome GDM Login Theme Manager'
            url nexusURL

            scm {
                connection nexusURL
                developerConnection nexusURL
                url nexusURL
            }

            licenses {
                license {
                    name 'GNU General Public License v3.0'
                    url 'https://opensource.org/licenses/GPL-3.0'
                }
            }

            developers {
                developer {
                    id 'juhaku'
                    name 'Juha Kukkonen'
                }
            }
        }
    }
}
